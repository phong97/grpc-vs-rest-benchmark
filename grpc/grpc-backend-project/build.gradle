plugins {
    id 'java'
    id 'org.springframework.boot' version '3.5.4'
    id 'io.spring.dependency-management' version '1.1.7'
    id 'com.google.protobuf' version '0.9.4'
}

group = 'com.grpc'
//version = '0.0.1-SNAPSHOT'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

repositories {
    mavenCentral()
}

ext {
    set('springGrpcVersion', "0.9.0")
    set('descriptorName', "message.pd")
}

dependencies {
    implementation 'io.grpc:grpc-services'
    implementation 'org.springframework.grpc:spring-grpc-spring-boot-starter'
    implementation 'org.springframework.boot:spring-boot-starter'
    implementation('org.springframework.boot:spring-boot-starter-web') {
        exclude group: 'org.springframework.boot', module: 'spring-boot-starter-tomcat'
    }
    implementation 'org.springframework.boot:spring-boot-starter-jetty'
    implementation 'org.springframework.boot:spring-boot-starter-actuator'
    implementation 'io.micrometer:micrometer-registry-prometheus'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.springframework.grpc:spring-grpc-test'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

dependencyManagement {
    imports {
        mavenBom "org.springframework.grpc:spring-grpc-dependencies:${springGrpcVersion}"
    }
}

protobuf {
    protoc {
        artifact = 'com.google.protobuf:protoc'
    }
    plugins {
        grpc {
            artifact = 'io.grpc:protoc-gen-grpc-java'
        }
    }
    generateProtoTasks {
        all().each { task ->
            // bật xuất file descriptor
            task.generateDescriptorSet = true
            // chỉ định đường dẫn và tên file descriptor; có thể đặt tên là message.pd hoặc .desc
            task.descriptorSetOptions.path = "${projectDir}/build/descriptors/${descriptorName}"
            // nếu cần tự chứa toàn bộ các import của proto, bật includeImports
            task.descriptorSetOptions.includeImports = true
            // nếu muốn giữ thông tin nguồn (số dòng, comment), bật includeSourceInfo
            task.descriptorSetOptions.includeSourceInfo = true
        }
        all()*.plugins {
            grpc {
                option '@generated=omit'
            }
        }
    }
}

jar {
    dependsOn bootJar
    enabled = false  // Disable plain JAR since we want the boot JAR
}

bootJar {
    archiveBaseName = 'grpc-backend'
    archiveClassifier = ''
    archiveFileName = 'grpc-backend.jar'  // Explicitly set the filename
}

tasks.named('test') {
    useJUnitPlatform()
}
